apply plugin: 'com.android.application'

version = "0.0.1"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.makina.osmnav"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode updateVersionCode(module.name)
        versionName version
        buildConfigField "String", "BUILD_DATE", "\"" + new Date().getTime() + "\""
    }

    buildTypes {
        debug {
            versionNameSuffix "." + getVersionCode(module.name) + "-DEV"
        }

        release {
            versionNameSuffix "." + getVersionCode(module.name)
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def appName = output.outputFile.name
                    appName = appName.replace(module.name, "OSMNav")
                    appName = appName.replace(".apk", "-" + defaultConfig.versionName + ".apk")
                    // noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, appName)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'

    compile 'org.osmdroid:osmdroid-android:4.3'
    compile 'org.slf4j:slf4j-simple:1.6.1'

    compile(name:'osmbonuspack_v5.4', ext:'aar')
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.google.code.gson:gson:2.3'

    testCompile 'junit:junit:4.12'
}
